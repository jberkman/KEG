@LAZYGLOBAL OFF.

DECLARE LOCAL FUNCTION MMULT {
    DECLARE LOCAL PARAMETER R1, R2, R3, X.
    RETURN V(VDOT(R1, X), VDOT(R2, X), VDOT(R3, X)).
}

DECLARE LOCAL FUNCTION VRCRS {
    DECLARE PARAMETER A, B.
    RETURN V(A:Y*B:Z - A:Z*B:Y, A:Z*B:X - A:X*B:Z, A:X*B:Y - A:Y*B:X).
}

DECLARE LOCAL FUNCTION VL2R {
    DECLARE LOCAL PARAMETER A.
    RETURN V(A:X, A:Z, A:Y).
}

DECLARE LOCAL FUNCTION SHIPFRAME {
    DECLARE LOCAL S IS LEXICON().
    SET S:I TO V(1, 0, 0).
    SET S:J TO V(0, 1, 0).
    SET S:K TO V(0, 0, 1).
    RETURN S.
}

DECLARE LOCAL FUNCTION INERTIALFRAME {
    DECLARE LOCAL N IS LEXICON().
    SET N:I TO VL2R(SOLARPRIMEVECTOR).
    SET N:K TO V(0, 0, 1).
    SET N:J TO VRCRS(N:K, N:I).
    RETURN N.
}

//DECLARE LOCAL FUNCTION FIXEDFRAME {
//    PARAMETER B IS BODY.
//    DECLARE LOCAL E IS LEXICON().
//}

DECLARE LOCAL FUNCTION PERIFOCALFRAME {
    DECLARE PARAMETER O IS OBT.

    DECLARE LOCAL CA IS COS(O:ARGUMENTOFPERIAPSIS).
    DECLARE LOCAL SA IS SIN(O:ARGUMENTOFPERIAPSIS).

    DECLARE LOCAL CL IS COS(O:LAN).
    DECLARE LOCAL SL IS SIN(O:LAN).

    DECLARE LOCAL CI IS COS(O:INCLINATION).
    DECLARE LOCAL SI IS SIN(O:INCLINATION).

    DECLARE LOCAL P IS LEXICON().
    SET P:I TO V( CA*CL - SA*CI*SL,  CA*SL + SA*CI*CL, SA*SI).
    SET P:J TO V(-SA*CL - CA*CI*SL, -SA*SL + CA*CI*CL, CA*SI).
    SET P:K TO V(SI*SL, -SI * CL, CI).
    RETURN P.
}

//TODO:
//DECLARE LOCAL FUNCTION ORBITFRAME {
//    PARAMETER OBT IS ORBIT.
//    DECLARE LOCAL O IS LEXICON().
//}

//TODO:
//DECLARE LOCAL FUNCTION ORBITVELOCITYFRAME {
//    PARAMETER OBT IS ORBIT.
//    DECLARE LOCAL V IS LEXICON().
//}

DECLARE LOCAL FUNCTION XFORMCOORD {
    DECLARE PARAMETER F, R.
    RETURN R:X*F:I + R:Y*F:J + R:Z*F:K.
}

DECLARE LOCAL FUNCTION XFORMFRAME {
    DECLARE PARAMETER DST, SRC.
    DECLARE LOCAL F IS LEXICON().
    SET F:I TO XFORMCOORD(DST, SRC:I).
    SET F:J TO XFORMCOORD(DST, SRC:J).
    SET F:K TO XFORMCOORD(DST, SRC:K).
    RETURN F.
}

CLEARVECDRAWS().

DECLARE LOCAL V0 IS V(0,0,0).

DECLARE LOCAL SCALE IS 1.
DECLARE LOCAL WIDTH IS 1.

DECLARE LOCAL SLEN IS 200000.
DECLARE LOCAL NLEN IS 2 * BODY:RADIUS.

DECLARE LOCAL SIVEC IS VECDRAW(V0, SLEN * V(1, 0, 0), RED, "", SCALE, TRUE, WIDTH).
DECLARE LOCAL SJVEC IS VECDRAW(V0, SLEN * V(0, 0, 1), GREEN, "", SCALE, TRUE, WIDTH).
DECLARE LOCAL SKVEC IS VECDRAW(V0, SLEN * V(0, 1, 0), BLUE, "", SCALE, TRUE, WIDTH).

DECLARE LOCAL NIVEC IS VECDRAW(V0, V0, RED, "", SCALE, TRUE, WIDTH).
DECLARE LOCAL NJVEC IS VECDRAW(V0, V0, GREEN, "", SCALE, TRUE, WIDTH).
DECLARE LOCAL NKVEC IS VECDRAW(V0, V0, BLUE, "", SCALE, TRUE, WIDTH).

DECLARE LOCAL PIVEC IS VECDRAW(V0, V0, RED, "", SCALE, TRUE, WIDTH).
DECLARE LOCAL PJVEC IS VECDRAW(V0, V0, GREEN, "", SCALE, TRUE, WIDTH).
DECLARE LOCAL PKVEC IS VECDRAW(V0, V0, BLUE, "", SCALE, TRUE, WIDTH).

DECLARE LOCAL RVEC IS VECDRAW(V0, V0, YELLOW, "", SCALE, TRUE, WIDTH).
// DECLARE LOCAL VVEC IS VECDRAW(V0, V0, MAGENTA, "V", SCALE, TRUE, WIDTH).

DECLARE LOCAL S IS SHIPFRAME().

UNTIL 0 {
    DECLARE LOCAL N IS INERTIALFRAME().

    SET RVEC:START TO BODY:POSITION.
    SET RVEC:VEC TO 1000000 * SOLARPRIMEVECTOR.

    SET NIVEC:START TO BODY:POSITION.
    SET NJVEC:START TO BODY:POSITION.
    SET NKVEC:START TO BODY:POSITION.

    SET NIVEC:VEC TO NLEN * VL2R(N:I).
    SET NJVEC:VEC TO NLEN * VL2R(N:J).
    SET NKVEC:VEC TO NLEN * VL2R(N:K).

    IF HASTARGET {
        DECLARE LOCAL P IS PERIFOCALFRAME(TARGET:OBT).
        DECLARE LOCAL PLEN IS TARGET:OBT:PERIAPSIS + TARGET:BODY:RADIUS.

        SET PIVEC:START TO BODY:POSITION.
        SET PJVEC:START TO BODY:POSITION.
        SET PKVEC:START TO BODY:POSITION.

        SET PIVEC:VEC TO VL2R(XFORMCOORD(N, PLEN * P:I)).
        SET PJVEC:VEC TO VL2R(XFORMCOORD(N, PLEN * P:J)).
        SET PKVEC:VEC TO VL2R(XFORMCOORD(N, PLEN * P:K)).
    }

    WAIT 1.
}
